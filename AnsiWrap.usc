start AnsiWrap(parmfile, option, retcode)
 %VERSION 0.3.000 08/18/2015
'Author:  R. Whaite, Helen Farabee Centers
'Create Date:  8/16/2011

parmfile          is x
option            is x
retcode           is b

win1              is b   'Window Handle number
rc                is i   'Function return codes
CreateBill        is b   'Error code from $misprog
FileName          is x   'Name of output file
CheckFile         is x   'Output file name & path
FilePath          is x   'path to output file
FileSize          is n
FileCreateDT      is d
FileCreateTM      is t
FileOwner         is x
BillParm          is x
FixCMD            is x   'Unix command for fixing output file
Version           is x   '5010 or 4010
ParmArray[]       is x   'Array for building a parameter file
VerParm           is x   'Parameter file name
bill_parm         is x   'Billing Parm file to use selected from either $adparm1 or $adparm2
tempfile          is x   'Temporary file for the FixCmd

tracepath         is x
allowoverwrite    is x

prog_type         is x
seqout            is x

dofield013        is x   'Flag to turn on using Field Record 102 Field 113.

'parameters for misprog
NSA837P5          is x
NSA837P5PARM      is x

'parameters for uscript 
claim_type        is x
'NSA837P5PARM      is x    'share this with misprog
parm_override[]   is x
er_keys[]         is x
usc-errors[]      is x
usc-out-item[]    is x
usc-out-value[]   is x


'Check to make sure the Add Parm 1 menu item has something there.  If not, give the error message and terminate the script.
if checkForValue(parmfile, "This Script Requires a Parmfile to work properly", "Please Contact MIS") > 0 then return endif

prog_type     = "MIS"
dofield013    = "Y"
claim_type    = "P5"

getparm(parmfile)
getoption(option)

if tracepath dp then
   $trace("path,on",tracepath)
endif

if checkForValue(NSA837P5PARM, "NSA837P5PARM missing from Parmfile", "Please Correct") > 0 then return endif
if checkForValue(NSA837P5PARM, "Billing Parmfile not specified", "Please Correct") > 0 then return endif

'check the billing parm file for the parameter "SEQOUT"
getparm(NSA837P5PARM)

'Check to see if the file is there.  If it is, give the error message and terminate the script.
'If it is not, allow the 837P proess to continue with the parm in Add Parm 1.
if seqout dp then
   FileName = seqout

   rc = $checkfile(FileName,FileSize,FileCreateDT,FileCreateTM,,,FileOwner)

   'If the file already exists, issue the error message and instructions to the user
   if rc = 0 and allowoverwrite != "Y" then  
   
      $errmsg1 = "FILE REPLACEMENT ERROR, " + FileName + " File Exists!"
      if $operioallow = "Y" then
         $openwin(win1, 5, 25, 4, 64)
         $disp("Script ID:", 6, 5,,"H")
         $disp($scriptid,    6,16,,)
         $disp(`"FILE REPLACEMENT ERROR, " + CheckFile + " File Exists!"`, 7, 5,,"H")
         $disp(`"File Size:  " + FileSize + " bytes"`, 9, 5,,"H")
         $disp(`"File Date:  " + FileCreateDT`, 10, 5,,"H")
         $disp(`"File Time:  " + FileCreateTM`, 11, 5,,"H")
         $disp(`"File Owner: " + FileOwner`, 12, 5,,"H")
         $disp("Print this Screen and Contact MIS", 19, 5,,"H")
         $acpt()
      endif
      return
   endif
endif
                

'safe to create a bill
select prog_type
   case "MIS"  
      CreateBill = $misprog(474,parmfile)

      if CreateBill > 0 then
         ' Show a error message
         select CreateBill
            case 1         $errmsg1 = "The mis-prog-ref isn't in the program control file"
            case 2         $errmsg1 = "Operator isn't authorized to initiate this program"
            case 3         $errmsg1 = "This function isn't permitted if $sessiontabs is active"
            case 4         $errmsg1 = "The program attempted is not allowed in $misprog (e.g. menu)"
            case 5         $errmsg1 = "$misprog cannot be executed while $setmis is in effect"
            case 6         $errmsg1 = "Program is browser only and session is in character mode"
            case 7         $errmsg1 = "Program is non-browser mode and session is in browser mode"
            case 8         $errmsg1 = "Error bidding the CMHC/MIS program"
            case 9         $errmsg1 = "Program control file error"
            case 10        $errmsg1 = "The session is a CRON, but the requested CMHC/MIS program cannot be run in CRON mode"
            case 11        $errmsg1 = "More than one left-frame menu referenced"
            case other     $errmsg1 = $format(rc, "Unknown error (99)")
         endselect

         return
      endif
   case "USCRIPT"
      CreateBill = $nsa837(claim_type, NSA837P5PARM, parm_override[], er_keys[], 
         usc-errors[], usc-out-item[], usc-out-value[])
      if createBill > 0 then
         select CreateBill
            case 1         $errmsg1 = "Errors With Bill Parmfile"
            case 2         $errmsg1 = "Invalid Claim-type"
            case 3         $errmsg1 = "Invalid parm-in argument"
            case 4         $errmsg1 = "Parameter name/item argument error"
            case 5         $errmsg1 = "Output name/item argument error"
            case 6         $errmsg1 = "Error argument invalid"
            case 7         $errmsg1 = "Event Key argument error"
            case 9         $errmsg1 = "TEMP file creation error"
         endselect
      endif
      rc = 0
      do while rc++ < $maxarray(usc-errors[])
         usc-errors[rc] = usc-errors[rc]
      enddo
      rc = 0
      do while rc++ < $maxarray(usc-out-value[])
         usc-out-item[rc] = usc-out-item[rc]
         usc-out-value[rc] = usc-out-value[rc]
      enddo
endselect

' Have a post fix command to run
if dofield013 = "Y"
   rc = $tempfile(tempfile)
   FixCmd = "sed 's/BK:ICD10:/ABK:/g' " + Filename + " > " + tempfile + " ; cp " + tempfile + " " + Filename
   rc = $unix(FixCmd)         
endif

end AnsiWrap

%include inc_GetParm
%include inc_GetOption

dynamic function checkForValue(key, msg1, msg2) is i
key      is v
msg1     is x
msg2     is x

checkForValue = 0
if key !DP then
   $errmsg1 = msg1
   $errmsg2 = msg2

   if $operioallow = "Y" then
      ShowMessageWin(msg1, , msg2)
   endif
   checkForValue = 1
   return
endif
end checkForValue

function ShowMessageWin(msg1, msg2, msg3, msg4) is null
'--- function arguments ---
msg1      is x   ' Message line one
msg2      is x   ' Message line two
msg3      is x   ' Message line three
msg4      is x    ' Message line four
' --- Local Variables ---
win1      is b   ' Window Number
'------------------------
$openwin(win1, 5, 12, 4, 64)
$disp("Script ID:", 6,  6,,"H")
$disp($scriptid,    6, 17,,)
$disp(msg1,  8, 6,,"H")
$disp(msg2,  9, 6,,"H")
$disp(msg3, 10, 6,,"H")
$disp(msg4, 11, 6,,"H")
$acpt()
end ShowMessageWin

